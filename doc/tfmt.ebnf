/* W3C EBNF Notation: */

Program  ::= ID "(" Parameters? ")" String? "{" Block "}"

ID       ::= [a-zA-Z][a-zA-Z0-9_]*

Parameters
         ::= Parameter ( "," Parameter )*

Parameter
         ::= ID ( "=" ( Integer | String ) )?

Block    ::= Expression*

Expression
         ::= Ternary ( "?" Ternary ":" Ternary )*

Ternary  ::= Disjunct ( ( "||" | "|" ) Disjunct )*

Disjunct ::= Conjunct ( ( "&&" | "&" ) Conjunct )*

Conjunct
         ::= Term ( ( "+" | "-" ) Term )*

Term     ::= Factor ( ( "*" | "/" | "%" ) Factor )*

Factor   ::= Exponent ( ( "**" | "^" ) Exponent )*

Exponent ::= "+" Exponent
           | "-" Exponent
           | "(" Expression+ ")"
           | Primary

Primary
         ::= Comment | Function | Integer | String | Substitution | Tag

Comment  ::= "/*" PrintableChars* "*/"
           | "//" PrintableChars* ( "\n" | EOF )

Function ::= "$" ID "(" ( Expression ( "," Expression )* )? ")"

Integer  ::= [0-9]+

String   ::= SingleQuote PrintableChars* SingleQuote
           | DoubleQuote PrintableChars* DoubleQuote
           | TripleSingleQuote ( PrintableChars | "\n" )* TripleSingleQuote
           | TripleDoubleQuote ( PrintableChars | "\n" )* TripleDoubleQuote

Symbol
         ::= "$(" ID ")"

Tag      ::= "<" ID ">"
